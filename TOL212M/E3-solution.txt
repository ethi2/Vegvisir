// Höfundur spurningar:  Snorri Agnarsson, snorri@hi.is

// Höfundur lausnar:     eti2
// Permalink lausnar:    https://tio.run/##vVZLb9tGED5bv2J6kgzTSpq@ZTtoizQIUAQNYN8MH1bmUlxrH/TuUoZk68f4UqCHAgYK9FAgvfCHuTO7JEUpcurARQlRJHceO49vZjZlmZ7f3z97Bm@qP7NSp6UFV5RWCz1hdgRwrI21An6YaGadMzoBF1a@z8VQuF5vTVKy0ukghhf34gWR33GrmBR6ukY@@fEVCT/pipv/ZmE8txfMQvXH2PK5Z1ryiYBcll6Am9rq1gqN@w6J//XcCgssQy6o/mZaBz4GHP2ubhshrpAoqjvgYkIPhnfUTUpIz0kuHODP5xycZ9aDycJH0XnPhOT4wjy43JQyBW08CY85nOdMT3g6hNdGSnOF4UZG1BfEa73hfW7KWpoklUlFNh8@OXIhdBfVrcQ9MiZlcDnDMGEWFT7QJZgImQqofofCTIPf8IbLAgOF2T73wujoGkqldRwwz5I7DzMmSw5CAwNVSi8c98Oe4j43KbwV@pfsbb06ADVqWQ6F9i9hFyz3CEA3IENGqMXDbo8gY/llKSx3oOCzo1bqehmIXLsy0HBX/Km1xcxY8nEBN3gTFUajwHl4BIvedeClz9FNK38QFmeIqtRoDqPOhsiyXJFVPxD3iS2uXuWUdlz/0Eq6Un5uOXNka79dFBp1CYa@Kjg6CnvubSdH@4hhC3XT0Wh7x1div24Fyf4FeR2j0j9YGVk7HQ1pnFgsVxy13/39rVSRoc7DsO91DC3ufgAxCsveMgDwp@lUgMKqi6WFJcedK4HwKALeXhmqmLpWYn002GvxdIKA2wajOq3koqPdj431A7UbTWTOcaytRmTgdinqao1Yx7JILjGazyl8BXp0ifeNu6GgutPijJbd6eXZAbnUIJwJPejur9bg8zz5PHmRfJF8mXyVfJ18k3ybfJdsWetA7H@xnyQLS9XmDmJ@/pveLKv303K9N7O652725sCfdRr0Y5syeCHpk5RUf/ms@tUyVDG2YlLdCXxDYcljz3IYx21NnOv0sS0c@zdJty38hHpfOXbYnzjG7yMtvJYhaawJY1P00BsQqpBckSyDGbeO7DTbjH560z9e0xmCajFyiDLq8tTtbanLkI6fJWWnDnRGQz/WqMcH1ehwi74YKXxznd5PPdzgdwzQOY@DWGjhcoScmVimVDsAPyjwAPyPzAkYgBuR7qbwCcnBMn9B1s/wDxHDNA6zZiBM6SiCqMwRcQ3/uxLDX6A9WAXM85b3nHjxmXPLezvtnNkovRXlk@qu7hIPmJvZUjnEtgynMcBSwK5rJtgQyPBhI4qLiCNdvaeTSHU3Q/g3hcExL6GMpkKy5gTzgMOYEC/wpLZgFP/gCKlGSyB6391QUII1v4IwesZYJGNU11ZTzE1LbCSHw2FvJ07NzXyOQv/aCb3u9OxRk7QZIbWvGUP35xYrXk9KkPPp9KLs8m5OEw7SmGI1PT9pQqv@XgcCD4/iR8Lg30c5zdtRs2obnXalbwEvSZk92xjyD2DLeZNpqvcYKM1WxdDFh16DyNik86YzhuCFoljJtPn1lMb1o57q4/DaaY4NsL/KqF82iXd7p77OPR4S7u//AQ

///////////////////////////////////////////////////////////////
// Hér byrjar óbreytanlegi hluti skrárinnar.
// Fyrir aftan þann hluta er sá hluti sem þið eigið að breyta.
//
// This is the start of the part of the file that should not
// be changed. Following this part is the part you should
// modify.
///////////////////////////////////////////////////////////////

// Hjálparfall sem finnur minnsta gildi í poka.
// Helper function that finds the smallest value in a multiset.
method MinOfMultiset( m: multiset<int> ) returns( min: int )
    requires m != multiset{}
    ensures min in m
    ensures forall z | z in m :: min <= z
{
    min :| min in m;
    var done := multiset{min};
    var m' := m-done;
    while m' != multiset{}
        decreases m'
        invariant m == done+m'
        invariant min in done
        invariant forall z | z in done :: min <= z
    {
        var z :| z in m';
        done := done+multiset{z};
        m' := m'-multiset{z};
        if z < min { min := z; }
    }
}

// Ekki má breyta þessu falli.
// Do not change this function.
method Test( m: multiset<int> )
{
    var s := Sort(m);
    assert multiset(s) == m;
    assert forall p,q | 0 <= p < q < |s| :: s[p] <= s[q];
}

method Main()
{
    var m := multiset{0,1,2,3,4,5,6,7,8,9,0,1,2,3,4,5,6,7,8,9};
    var s := Sort(m);
    assert multiset(s) == m;
    assert forall p,q | 0 <= p < q < |s| :: s[p] <= s[q];
    print s;
}

///////////////////////////////////////////////////////////////
// Hér lýkur óbreytanlega hluta skrárinnar.
// Hér fyrir aftan er sá hluti sem þið eigið að breyta til að
// útfæra afbrigði af selection sort.
//
// This is the end of the part of the file that should not be
// changed. The subsequent part is the part you should change
// in order to implement a version of selection sort.
///////////////////////////////////////////////////////////////

// Selection sort sem raðar poka í runu.
// Klárið að forrita þetta fall.
// Selection sort that sorts a multiset into a sequence.
// Finish programming this function.
method Sort( m: multiset<int> ) returns ( s: seq<int> )
    // Setjið viðeigandi ensures klausur hér
    // Put appropriate ensures clauses here
	ensures multiset(s) == m
	ensures forall p,q | 0 <= p < q < |s| :: s[p] <= s[q]
{
    // Setjið viðeigandi frumstillingar á m' og s hér.
    // m' er ný staðvær breyta en s er skilabreyta.
    // Put appropriate initializations for m' and s here.
    // m' is a new variable but s is the return variable.
    //...
	var m': multiset<int> := m;
	s := [];
    while m' != multiset{}
        // Ekki breyta fastayrðingu lykkju
        // Do not change the loop invariant
        decreases m'
        invariant m == m'+multiset(s)
        invariant forall p,q | 0 <= p < q < |s| :: s[p] <= s[q]
        invariant forall z | z in m' :: forall r | 0 <= r < |s| :: z >= s[r]
    {
        // Setjið viðeigandi stofn í lykkjuna hér
        // Put an appropriate body of the loop here
        //...
	var t := MinOfMultiset(m');
	m' := m' - multiset{t};
	s := s+[t];
    }
}